plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
}

// Load keystore properties
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    namespace 'com.postmuse.screenshare'
    compileSdk 34
      defaultConfig {          
        applicationId "com.postmuse.screenshare"        
        minSdk 26
        targetSdk 34
        versionCode 3
        versionName "1.0.5"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    
    dependenciesInfo {
        // Disables dependency metadata when building APKs as requested by F-Droid
        includeInApk = false
        // Disables dependency metadata when building Android App Bundles
        includeInBundle = false
    }
    
    signingConfigs {
        release {            if (keystorePropertiesFile.exists()) {
                storeFile rootProject.file(keystoreProperties['storeFile']) // Use rootProject.file()
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            } else {
                println("Warning: keystore.properties not found. Release build will not be signed.")
                // Optionally configure a dummy signing config or throw an error
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    
    compileOptions {
        // Updated to Java 17 for better compatibility with Gradle 9.0
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
}

dependencies {
    implementation(libs.androidx.core)
    implementation(libs.androidx.appcompat)
    implementation(libs.material)
    implementation(libs.androidx.constraintlayout)
    implementation(libs.okhttp)
    testImplementation(libs.junit)
    androidTestImplementation(libs.androidx.test.ext)
    androidTestImplementation(libs.androidx.test.espresso)
    implementation(libs.androidx.lifecycle.runtime)
    
    // Add security library for encrypted storage
    implementation("androidx.security:security-crypto:1.1.0-alpha06")
}
